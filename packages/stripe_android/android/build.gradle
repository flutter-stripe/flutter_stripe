group 'com.flutter.stripe'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.8.0'
    ext.stripe_version = '20.48.+'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace 'com.flutter.stripe'
    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
        consumerProguardFiles 'proguard-rules.txt'
    }
}

dependencies {
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "com.stripe:stripe-android:$stripe_version"
    implementation "com.stripe:financial-connections:$stripe_version"
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    // play-services-wallet is already included in stripe-android
    compileOnly "com.google.android.gms:play-services-wallet:19.3.0"

    // Users need to declare this dependency on their own, otherwise all methods are a no-op
    compileOnly 'com.stripe:stripe-android-issuing-push-provisioning:1.1.0'
}

afterEvaluate {
    // check version of com.android.application - fail if 8.0.0 or higher
    def androidPluginVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    print("android plugin version: " + androidPluginVersion)
    if (androidPluginVersion >= '8.0.0') {
        throw new GradleException("flutter_stripe: Stripe Android SDK requires com.android.application plugin lower than 8.0.0.\nSee https://github.com/flutter-stripe/flutter_stripe/issues/1909\n")
    }
}
