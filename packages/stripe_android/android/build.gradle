group 'com.flutter.stripe'
version '1.0-SNAPSHOT'

buildscript {
    def kotlin_version = rootProject.ext.has('kotlinVersion')
        ? rootProject.ext.get('kotlinVersion')
        : '1.9.0'

    ext.kotlin_version = kotlin_version
    ext.stripe_version = '21.26.+'
    ext.compose_version = '1.5.1'

    def kotlinMajor = kotlin_version.tokenize('\\.')[0].toInteger()

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // only use compose-compiler plugin when Kotlin >= 2.0
        if (kotlinMajor >= 2) {
            classpath "org.jetbrains.kotlin:compose-compiler-gradle-plugin:$kotlin_version"
        }
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

def kotlin_version = rootProject.ext.has('kotlinVersion')
    ? rootProject.ext.get('kotlinVersion')
    : '1.9.0'
def kotlinMajor = kotlin_version.tokenize('\\.')[0].toInteger()

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
// Only apply compose plugin if Kotlin >= 2.0
if (kotlinMajor >= 2) {
    apply plugin: 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'com.flutter.stripe'
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.1'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
        consumerProguardFiles 'proguard-rules.txt'
    }
}

dependencies {
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "com.stripe:stripe-android:$stripe_version"
    implementation "com.stripe:financial-connections:$stripe_version"
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    // Jetpack Compose dependencies for EmbeddedPaymentElement
    implementation platform('androidx.compose:compose-bom:2023.10.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.runtime:runtime'
    implementation 'androidx.activity:activity-compose:1.8.0'

    // play-services-wallet is already included in stripe-android
    compileOnly "com.google.android.gms:play-services-wallet:19.3.0"

    // Users need to declare this dependency on their own, otherwise all methods are a no-op
    compileOnly 'com.stripe:stripe-android-issuing-push-provisioning:1.1.0'
}
